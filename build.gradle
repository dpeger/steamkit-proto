task wrapper(type: Wrapper) { gradleVersion = '2.12' }


buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.7.6'
    }
}

apply from: "${rootProject.rootDir}/gradle/publish.gradle"
apply from: "${rootProject.rootDir}/gradle/bintray.gradle"

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.google.protobuf'
    apply plugin: 'eclipse'

    sourceCompatibility = 1.8

    group = 'de.peger.steamkit'
    version = '1.0.0-beta-1'

    repositories { mavenCentral() }

    dependencies { compile 'com.google.protobuf:protobuf-java:3.0.0-beta-2' }

    protobuf {
        protoc { artifact = 'com.google.protobuf:protoc:3.0.0-beta-2' }
    }

    jar {
        exclude '**/*.proto' 
        manifest.attributes provider: 'Daniel H. Peger'
    }
    
    task sourceJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allJava
        from protobuf.generatedFilesBaseDir + '/main/java'
    }
    
    task protoJar(type: Jar) {
        classifier = 'protobuf'
        from sourceSets.main.proto
    }
    
    artifacts {
        archives sourceJar
        archives protoJar
    }
}